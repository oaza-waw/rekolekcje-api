buildscript {
  ext {
    springBootVersion = '1.5.4.RELEASE'
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath('org.springframework:springloaded:1.2.6.RELEASE')
    classpath 'org.liquibase:liquibase-core:3.5.3'
    classpath 'org.postgresql:postgresql:42.1.4'
  }
}

plugins {
  id 'org.sonarqube' version '2.5'
  id 'org.liquibase.gradle' version '1.2.4'
  id 'io.franzbecker.gradle-lombok' version '1.11'
}

repositories {
  mavenCentral()
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'idea'
apply plugin: 'jacoco'

idea {
  module {
    inheritOutputDirs = false
    outputDir = file(project.buildDir.toString() + '/classes/main/')
  }
}

liquibase {
  activities {
    main {
      changeLogFile 'src/main/resources/changelogs.postgresql.sql'
      url 'jdbc:postgresql://localhost:5432/rekolekcjedb'
      username 'postgres'
      password 'postgres'
    }
  }
}

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8


dependencies {
  compile 'org.springframework.boot:spring-boot-starter-data-jpa'
  compile 'org.springframework.boot:spring-boot-starter-data-rest'
  compile 'org.springframework.boot:spring-boot-starter-web'
  compile 'com.h2database:h2'
  compile 'org.postgresql:postgresql'
  compile 'org.json:json:20160810'

  compile 'org.springframework.boot:spring-boot-starter-security'
  compile 'io.jsonwebtoken:jjwt:0.7.0'

  compile 'com.google.gag:gag:1.0.1'

  runtime('org.springframework.boot:spring-boot-devtools')

  testCompile('org.springframework.boot:spring-boot-starter-test') {
    exclude group:'com.vaadin.external.google', module: 'android-json'
  }
  testCompile 'org.springframework.security:spring-security-test:5.0.0.RELEASE'
  testCompile 'org.assertj:assertj-core:3.8.0'
  testCompile 'nl.jqno.equalsverifier:equalsverifier:2.3.3'
}

sourceSets {
  testIntegration {
    java {
      compileClasspath += main.output
      compileClasspath += main.compileClasspath
      compileClasspath += test.output
      compileClasspath += test.compileClasspath
      runtimeClasspath += test.runtimeClasspath
    }
  }
}

task testIntegration(type: Test) {
  testClassesDir = sourceSets.testIntegration.output.classesDir
  classpath = sourceSets.testIntegration.runtimeClasspath
}

processResources {
  from('../rekolekcje-webapp/dist') {
    //Public is a default supported Spring Boot resources directory.
    into 'public'
  }
}

//frontend:build will be run before the processResources
processResources.dependsOn(':rekolekcje-webapp:build')

jacocoTestReport {
  reports {
    xml.enabled true
  }
}

check.dependsOn jacocoTestReport

bootRun {
  systemProperties System.properties
}
